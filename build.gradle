plugins {
  id 'java'
  id 'application'
  id 'maven'
  id 'com.github.johnrengelman.shadow' version '2.0.1'
  id "com.github.hierynomus.license" version "0.14.0"
}

ext {
  vertxVersion = '3.5.0'
}

repositories {
  mavenLocal()
  jcenter()
  maven { url "https://plugins.gradle.org/m2/" }
}

version = '1.0.0-SNAPSHOT'

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
      "-proc:only",
      "-processor", "io.vertx.codegen.CodeGenProcessor",
      "-Acodegen.output=${project.projectDir}/src/main"
  ]
}

compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8

  dependsOn annotationProcessing
}

sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
}
dependencies {
  compile "io.vertx:vertx-unit:$vertxVersion"
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile "io.vertx:vertx-rx-java2:$vertxVersion"
  compile "io.vertx:vertx-codegen:$vertxVersion"
  compile "io.vertx:vertx-service-proxy:$vertxVersion"
  compile "org.m1theo:tinkerforge-xcore:0.0.1-SNAPSHOT"
  compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

mainClassName = 'org.m1theo.tinkerforge.server.MainVerticle'

shadowJar {
  classifier = 'fat'
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainClassName, "--launcher-class=io.vertx.core.Launcher"]
}

license {
  header rootProject.file('HEADER')
  skipExistingHeaders false
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
}
